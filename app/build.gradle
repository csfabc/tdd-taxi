apply plugin: 'com.android.application'
apply plugin: 'jacoco'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.jiker.keju"
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.0-alpha-2'
    androidTestImplementation 'com.jayway.android.robotium:robotium-solo:5.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

jacoco {
    toolVersion = "0.7.1.201405082137"   //版本号可用最新
}
def coverageSourceDirs = [
        '../app/src/main/java' // deckard-master需要修改为你的module名称
]
task jacocoTestReport(type:JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports"
    // 下面dir deckard-master需要修改为你的module名称
    classDirectories = fileTree(
            dir: '../app/build/intermediates/javac/debug/classes',
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/*$ViewInjector*.*',
                       '**/BuildConfig.*',
                       '**/Manifest*.*',
                        '**/MainActivity.class']          // 去掉不进行检查的文件
    )
    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    // 下面dir deckard-master需要修改为你的module名称
    executionData = files('../app/build/jacoco/testDebugUnitTest.exec')
    reports {
        xml.enabled = true
        html.enabled = true
    }
}